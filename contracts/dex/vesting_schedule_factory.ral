Contract VestingScheduleFactory(
  vestingScheduleTemplateId: ByteVec
) extends Math() {
  event VestingScheduleCreated(schedule: ByteVec, token: ByteVec, beneiciary: Address, amount: U256, start: U256, duration: U256)

  @using(preapprovedAssets = true)
  pub fn createSchedule(
    payer: Address,
    tokenId: ByteVec,
    amount: U256,
    beneficiary: Address,
    duration: U256
  ) -> () {
    assert!(duration > 0, 1) // Nit: Better introduce explicit error codes
    assert!(amount > 0, 1) // Nit: Better introduce explicit error codes
    
    let start = blockTimeStamp!()
    // We have introduced this helper function since web3 0.11.x
    let (encodedImmFields, encodedMutFields) = VestingSchedule.encodeFields!(tokenId, amount, beneficiary, start, duration)

    let scheduleId = copyCreateSubContract!{payer -> ALPH: 1 alph, tokenId: amount}(
      encodeToByteVec!(beneficiary), // we could use `toByteVec!(address)` which is much more efficient.
      vestingScheduleTemplateId,
      encodedImmFields,
      encodedMutFields
    )

    emit VestingScheduleCreated(scheduleId, tokenId, beneficiary, amount, start, duration)
  }

  pub fn getVestingScheduleByAddress(address: Address)-> VestingSchedule {
    let vestingScheduleId = subContractId!(encodeToByteVec!(address)) // we could use `toByteVec!(address)` which is much more efficient.
    
    return VestingSchedule(vestingScheduleId)
  }
}
