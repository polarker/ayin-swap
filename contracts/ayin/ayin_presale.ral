Contract AyinPresale (
    ayinToken: ByteVec,
    mut tokensForSale: U256,
    mut alphPerToken: U256,
    mut saleOpen: Bool,
    mut tokensSold: U256,
    mut alphBalance: U256,
    mut owner_: Address
) extends Permissions(owner_) {
    enum ErrorCodes {
        SaleNotOpen = 0
        NotEnoughTokens = 1
    }

    pub fn getAyinTokenId() -> ByteVec {
        return ayinToken
    }

    pub fn getAlphBalance() -> U256 {
        return alphBalance
    }

    pub fn getTokensLeft() -> U256 {
        return tokensForSale - tokensSold
    }

    pub fn getAlphPerToken() -> U256 {
        return alphPerToken
    }

    pub fn getSaleOpen() -> Bool {
        return saleOpen
    }

    // checkExternalCaller is true for public functions by default
    @using(updateFields = true)
    pub fn setSaleOpen(open: Bool) -> () {
        onlyOwner(callerAddress!())
        saleOpen = open
    }

    // checkExternalCaller is true for public functions by default
    @using(updateFields = true)
    pub fn setAlphPerToken(apt: U256) -> () {
        onlyOwner(callerAddress!())
        alphPerToken = apt
    }

    @using(assetsInContract = true, preapprovedAssets = true, updateFields = true, checkExternalCaller = false)
    pub fn buy(amount: U256) -> () {
        assert!(saleOpen, ErrorCodes.SaleNotOpen)
        assert!(getTokensLeft() - amount >= 0, ErrorCodes.NotEnoughTokens)

        let totalPrice = amount * alphPerToken / 1e18
        transferTokenToSelf!(callerAddress!(), ALPH, totalPrice)

        alphBalance = alphBalance + totalPrice
        tokensSold = tokensSold + amount

        if (tokensSold == tokensForSale) {
            saleOpen = false
        }

        transferTokenFromSelf!(callerAddress!(), ayinToken, amount)
    }

    @using(assetsInContract = true)
    fn withdrawToken_(caller: Address, tokenId: ByteVec, amount: U256, sendTo: Address) -> () {
        // onlyOwner(callerAddress!()) // callerAddress!() is `withdrawAlph` or `withdrawAyin`
        onlyOwner(caller)

        transferTokenFromSelf!(sendTo, tokenId, amount)
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn withdrawAlph(amount: U256, sendTo: Address) -> () {
        withdrawToken_(callerAddress!(), ALPH, amount, sendTo)

        alphBalance = alphBalance - amount
    }

    @using(checkExternalCaller = true, updateFields = true)
    pub fn withdrawAyin(amount: U256, sendTo: Address) -> () {
        withdrawToken_(callerAddress!(), ayinToken, amount, sendTo)

        tokensForSale = tokensForSale - amount
    }

    @using(assetsInContract = true, checkExternalCaller = true)
    pub fn destroy(remainingBalancesTo: Address) -> () {
        onlyOwner(callerAddress!())

        destroySelf!(remainingBalancesTo)
    }
}
